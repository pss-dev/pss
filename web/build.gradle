buildscript {
   repositories {
      jcenter()
      mavenCentral()
   }
}

plugins {
   id "com.github.node-gradle.node" version "2.2.3"
}

node {
   version = '10.17.0'
   npmVersion = '6.11.3'
   download = true
   workDir = file("${buildDir}/tools/nodejs")
}

task compileDevelopment(type: NpmTask, dependsOn: npmInstall) {
   onlyIf { !productionBuild }
   group 'build'
   description = 'Compiles applications using development settings'
   args = ['run', 'build']
}

task compileProduction(type: NpmTask, dependsOn: npmInstall) {
   onlyIf { productionBuild }
   group 'build'
   description = 'Compiles the applications using production settings'
   args = ['run', 'build:prod']
}

task compile(dependsOn: npmInstall) {
   group 'build'
   description 'Compile vue'
   dependsOn compileDevelopment, compileProduction
}

task lint(type: NpmTask, dependsOn: npmInstall) {
   group 'verification'
   description = 'Runs the linter on source code'
   args = ['run', 'lint']
}

task watchDev(type: NpmTask, dependsOn: npmInstall) {
   onlyIf { !productionBuild }
   group 'verification'
   description = 'Runs the watch on develop mode'
   args = ['run', 'build:watch']
}

task watchProd(type: NpmTask, dependsOn: npmInstall) {
   onlyIf { productionBuild }
   group 'verification'
   description = 'Runs the watch on product mode'
   args = ['run', 'build:prod:watch']
}

task watch() {
   group 'build'
   description 'Watch source'
   dependsOn watchDev, watchProd
}

task build(dependsOn: [lint, compile]) {
   group 'build'
   description = 'Build web'
}

task cleanChunk(type: Delete) {
   group 'build'
   delete = ['dist', 'build']
}

task clean(dependsOn: [cleanChunk]) {
   group 'build'
}

task cleanAll(type: Delete, dependsOn: [clean]) {
   group 'build'
   delete = ['node_modules']
}
